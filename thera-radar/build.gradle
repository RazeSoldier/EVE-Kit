version = "0.4.4"

buildscript {
    repositories {
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.beryx:badass-jlink-plugin:2.17.1"
    }
}

apply plugin: 'application'
apply plugin: 'org.beryx.jlink'

ext.moduleName = 'razesoldier.thera.radar'

application {
    mainClassName = "razesoldier.thera.radar.Daemon"
    applicationDefaultJvmArgs = [
            '-Dfile.encoding=UTF-8',
            '-Xmx64m'
    ]
}

dependencies {
    // Used to parse Json
    implementation 'com.alibaba:fastjson:1.2.61'

    // Logging system
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha1'

    // Used to send mail
    implementation 'org.jodd:jodd-mail:5.1.3'
    implementation 'com.sun.mail:jakarta.mail:1.6.4'
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jlink {
    addOptions('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')
    launcher {
        name = 'thera-radar'
    }
    mergedModule {
        additive = true
        excludeProvides servicePattern: 'javax.ws.rs.ext.*'
        excludeProvides service: 'org.glassfish.jersey.internal.spi.AutoDiscoverable'
    }
}
